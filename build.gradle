buildscript {
    repositories {
        mavenCentral();
        mavenRepo urls: 'http://download.eclipse.org/jgit/maven'
    }
    dependencies {
        classpath 'org.eclipse.jgit:org.eclipse.jgit:0.10.1'
    }
}

import org.eclipse.jgit.*;
import org.eclipse.jgit.api.*;
import org.eclipse.jgit.lib.*;
import org.eclipse.jgit.revwalk.*;
import org.eclipse.jgit.treewalk.*;
import org.eclipse.jgit.transport.*;

mavenRepository = file('repos')
remote = 'origin'
branch = 'gh-pages'



subprojects {

    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven'

    group = 'com.bluepapa32.gradle.plugins'
    version = '0.1.0'
    archivesBaseName = "gradle-${name}-plugin"

    repositories {
        mavenCentral()
    }

    dependencies {
        compile gradleApi()
        groovy localGroovy()
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: uri(mavenRepository))
            }
        }
    } << {
        def repos = new RepositoryBuilder()
                        .setGitDir(new File(mavenRepository, ".git"))
                        .setWorkTree(mavenRepository)
                        .readEnvironment()
                        .build();

        def git = new Git(repos);
        git.add().addFilepattern(".").call()
        git.commit().setMessage("${archivesBaseName} ${version} release").call();
        git.push().setRemote(remote).setRefSpecs(new RefSpec("${branch}:${branch}")).call()

        repos.close()
    }
}
